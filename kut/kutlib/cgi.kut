// Copyright 03-Mar-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Cgi management.

/// Standard password length.
klen = 300;

// Seconds == 30 days
tNoExpiration = 2592000;

// First key.
demeKey =
  "nkXliX8lg2kTuQSS/OoLXCk8eS4Fwmc+N7l6TTNgzM1vdKewO0cjok51vcdl" +
  "OKVXyPu83xYhX6mDeDyzapxL3dIZuzwyemVw+uCNCZ01WDw82oninzp88Hef" +
  "bn3pPnSMqEaP2bOdX+8yEe6sGkc3IO3e38+CqSOyDBxHCqfrZT2Sqn6SHWhR" +
  "KqpJp4K96QqtVjmXwhVcST9l+u1XUPL6K9HQfEEGMGcToMGUrzNQxCzlg2g+" +
  "Hg55i7iiKbA0ogENhEIFjMG+wmFDNzgjvDnNYOaPTQ7l4C8aaPsEfl3sugiw";

/// Key for responding when session is expired.
noSessionKey = "nosession";

/// Cgi home directory. Must be initialized with 'cgi.init'.
Home = [""];

// File password.
// s
fkey = cryp.key(demeKey, str.len(demeKey));

// Communication key.
ComKey = [""];

// Expiration in seconds. Must be initialized with 'cgi.init'.
// [i]
TExpiration = [0];

// USERS

// Returns a new user.
// \s, s, s -> m
userNew = \id, pass, level ->
  return {
    "id"   : id,
    "pass" : pass,
    "level": level
  };
;

// \m -> s
userToJs = \U ->
  return js.wa([
    js.ws(U.id),
    js.ws(U.pass),
    js.ws(U.level)
  ]);
;

// \s -> m
userFromJs = \s -> {
  A = js.ra(s);
  return userNew(
    js.rs(A[0]),
    js.rs(A[1]),
    js.rs(A[2])
  );
};

// \m -> ()
writeUsers = \Us -> {
  j = js.wa(arr.mp(Us, \U -> return userToJs(U);));
  file.write(path.cat([Home[0], "users.db"]), cryp.encode(fkey, j));
};

// \-> [m]
readUsers = \-> {
  j = cryp.decode(fkey, file.read(path.cat([Home[0], "users.db"])));
  return arr.mp(js.ra(j), \s -> return userFromJs(s););
};

// SESSIONS

// Returns a new session
// \s, s, s, s, s, i, i -> m
sessionNew = \id, comKey, conKey, user, level, tm, lapse ->
  return {
    "id": id,
    "comKey": comKey,
    "conKey": conKey,
    "user"  : user,
    "level" : level,
    "tm"    : tm,
    "lapse" : lapse
  };
;

// \m -> s
sessionToJs = \S ->
  return js.wa([
    js.ws(S.id),
    js.ws(S.comKey),
    js.ws(S.conKey),
    js.ws(S.user),
    js.ws(S.level),
    js.wi(S.tm),
    js.wi(S.lapse)
  ]);
;

// \s -> m
sessionFromJs = \s -> {
  A = js.ra(s);
  return sessionNew(
    js.rs(A[0]),
    js.rs(A[1]),
    js.rs(A[2]),
    js.rs(A[3]),
    js.rs(A[4]),
    js.ri(A[5]),
    js.ri(A[6])
  );
};

// If 's' is in time, returns a new session with 's.tm' updated. Otherwise
// return [].
// \m -> [m]
updateSession = \S -> {
  now = time.now() / 1000;
  if (S.tm + S.lapse < now) return [];
  return [sessionNew(S.id, S.comKey, S.conKey, S.user, S.level, now, S.lapse)];
};

// \m -> ()
writeSessions = \Ss -> {
  j = js.wa(arr.mp(Ss, \S -> return sessionToJs(S);));
  file.write(path.cat([Home[0], "sessions.db"]), cryp.encode(fkey, j));
};

// \-> [m]
readSessions = \-> {
  j = cryp.decode(fkey, file.read(path.cat([Home[0], "sessions.db"])));
  return arr.mp(js.ra(j), \s -> return sessionFromJs(s););
};

// Adds session and purges sessions.
// \s, s, s, s, s, i -> ()
addSession = \sessionId, comKey, conKey, user, level, lapse -> {
  now = time.now() / 1000;
  NewSessions = arr.filter(readSessions(), \S -> return S.tm + S.lapse >= now;);
  arr.push(
    NewSessions,
    sessionNew(sessionId, comKey, conKey, user, level, now, lapse)
  );
  writeSessions(NewSessions);
};

// Replace one session
// \m -> ()
replaceSession = \S -> {
  NewSessions = arr.filter(readSessions(), \OldS -> return OldS.id != S.id;);
  arr.push(NewSessions, S);
  writeSessions(NewSessions);
};

// PUBLIC FUNCTIONS --------------------------------------------------------

/// Initialize cgi.
///   homeDir: Absolute path of 'cgi' home directory.
///   texpiration: Expiration time in seconds.
/// \s, i-> ()
init = \homeDir, texpiration-> {
  Home[0] = homeDir;
  TExpiration[0] = texpiration;

  if (!file.exists(homeDir)) {
    file.mkdir(homeDir);
    writeUsers([]);
    putUser("admin", demeKey, "0");
    writeSessions([]);
  }
};

// USERS

/// Adds or modifies an user.
/// \s, s, s -> ()
putUser = \id, pass, level -> {
  pass = cryp.key(pass, klen);
  Users = readUsers();
  NewUser = userNew(id, pass, level);
  add = [true];
  for (U, i : Users) {
    if (U.id == id) {
      add = [false];
      Users[i] = NewUser;
      break;
    }
  }
  if (add[0]) arr.push(Users, NewUser);
  writeUsers(Users);
};

/// Removes an user.
/// \s -> ()
delUser = \id -> {
  Users = readUsers();
  writeUsers(arr.filter(Users, \U -> return U.id != id;));
};

/// Check user password and returns:
///   - If user exsits and password is correct, the user level.
///   - Otherwise, an empty string.
/// \s, s -> s
checkUser = \id, pass -> {
  pass = cryp.key(pass, klen);
  for (U : readUsers()) {
    if (U.id == id & U.pass == pass) return U.level;
  }
  return "";
};

// MANAGEMENT

/// Sends to client 'communicationKey', 'userId' and 'userLevel'. If
/// conection fails every one is "".
///   sessionId: Session identifier.
///   return   : Response with next fields:
///                 key   : String
///                 conKey:String
///                 user  : String
///                 level : String.
/// \s -> s
connect = \sessionId -> {
  ComKey[0] = sessionId;
  failRp = rp({
    "key": js.ws(""),
    "conKey": js.ws(""),
    "user": js.ws(""),
    "level": js.ws("")
  });
  S = arr.find(readSessions(), \S -> return S.id == sessionId;);
  if (arr.size(S) > 0) {
      S = updateSession(S[0]);
      if (arr.size(S) > 0) {
        S = S[0];
        conKey = cryp.genK(klen);
        replaceSession(sessionNew(
          S.id, S.comKey, conKey, S.user, S.level, S.tm, S.lapse
        ));
        return rp({
          "key": js.ws(S.comKey),
          "conKey": js.ws(conKey),
          "user": js.ws(S.user),
          "level": js.ws(S.level)
        });
      } else return failRp;
  } else return failRp;
};

/// Sends to client 'sessionId', 'communicationKey' and 'userLevel'. If
/// conection fails every one is "".
///   key           : Communication key
///   user          : User id.
///   pass          : User password.
///   withExpiration: If is set to false, session will expire after 30 days.
///   return        : Response with next fields:
///                     sessionId: String
///                     key      : String
///                     conKey   : String
///                     level    : String
/// \s, s, s, b -> s
authentication = \key, user, pass, withExpiration -> {
  ComKey[0] = key;
  level = checkUser(user, pass);

  if (level != "") {
    sessionId = cryp.genK(klen);
    newComKey = cryp.genK(klen);
    conKey = cryp.genK(klen);
    lapse = withExpiration ? TExpiration[0] : tNoExpiration;

    addSession(sessionId, newComKey, conKey, user, level, lapse);

    return rp({
      "sessionId": js.ws(sessionId),
      "key": js.ws(newComKey),
      "conKey": js.ws(conKey),
      "level": js.ws(level)
    });
  }
  return rp({
    "sessionId": js.ws(""),
    "key": js.ws(""),
    "conKey": js.ws(""),
    "level": js.ws("")
  });
};

/// Returns the session communication key if ssId is a valid session.
///   ssId  : Session identifier.
///   conKey: Connection key. If its value is "", this parameter is not used.
/// \s, s -> [s]
getComKey = \ssId, conKey -> {
  S = arr.find(readSessions(), \S -> return S.id == ssId;);
  if (arr.size(S) > 0) {
    if (conKey != "" & conKey != S[0].conKey) return [];
    S = updateSession(S[0]);
    if (arr.size(S) > 0) {
      ComKey[0] = S[0].comKey;
      return [ComKey[0]];
    } else return [];
  } else return [];
};

/// Changes user password.
/// The operation will fail if 'user' authentication fails.
///    user   : User id to change password.
///    oldPass: Old password.
///    newPass: New password.
///    return : Response with next field:
///               ok: Bool ('true' if operation succeds)
/// \s, s, s -> s
changePass = \user, oldPass, newPass -> {
  Ok = [false];
  Us = readUsers();
  for (U, i : Us) {
    if (U.id == user) {
      if (cryp.key(oldPass, klen) == U.pass) {
        Us[i] = userNew(U.id, cryp.key(newPass, klen), U.level);
        writeUsers(Us);
        Ok[0] = true;
      }
      break;
    }
  }
  return rp({"ok": js.wb(Ok[0])});
};

/// Deletes 'sessionId' and returns an empty response.
///   sessionId: Session identifier.
/// \s -> s
delSession = \sessionId -> {
  writeSessions(arr.filter(readSessions(), \S -> return S.id != sessionId;));
  return rpEmpty();
};

// RESPONSES

/// Returns a generic response to send to client.
///  Rp: Response. It is a Json object.
///      Example of reponse:
///        {"error": js.ws(msg)}
/// \m -> s
rp = \Rp -> return cryp.encode(ComKey[0], js.wo(Rp));;

/// Returns an empty response.
/// \-> s
rpEmpty = \-> return rp({});;

/// Returns a message with an only field "error" with value 'msg'.
///   msg: Error message.
/// \s -> s
rpError = \msg -> return rp({"error": js.ws(msg)});;

/// Returns a message with an only field "expired" with value 'true',
/// codified with the key 'noSessionKey' ("nosession")
/// \-> s
rpExpired = \-> {
  ComKey[0] = noSessionKey;
  return rp({"expired": js.wb(true)});
};

